# Maintainer: Evgeniy Alekseev

pkgname='ahriman'
pkgver=2.15.2
pkgrel=1
pkgdesc="ArcH linux ReposItory MANager"
arch=('any')
url="https://github.com/arcan1s/ahriman"
license=('GPL3')
depends=('devtools>=1:1.0.0' 'git' 'pyalpm' 'python-bcrypt' 'python-inflection' 'python-pyelftools' 'python-requests')
makedepends=('python-build' 'python-hatchling' 'python-installer' 'python-wheel')
optdepends=('python-aioauth-client: web server with OAuth2 authorization'
            'python-aiohttp: web server'
            'python-aiohttp-apispec>=3.0.0: web server'
            'python-aiohttp-cors: web server'
            'python-aiohttp-jinja2: web server'
            'python-aiohttp-security: web server with authorization'
            'python-aiohttp-session: web server with authorization'
            'python-boto3: sync to s3'
            'python-cerberus: configuration validator'
            'python-cryptography: web server with authorization'
            'python-matplotlib: usage statistics chart'
            'python-requests-unixsocket2: client report to web server by unix socket'
            'python-jinja: html report generation'
            'python-systemd: journal support'
            'rsync: sync by using rsync')
source=("https://github.com/arcan1s/ahriman/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"
        'ahriman.sysusers'
        'ahriman.tmpfiles')
install="$pkgname.install"
backup=('etc/ahriman.ini'
        'etc/ahriman.ini.d/logging.ini')

build() {
    cd "$pkgname-$pkgver"

    python -m build --wheel --no-isolation
}

package() {
      cd "$pkgname-$pkgver"

      python -m installer --destdir="$pkgdir" "dist/$pkgname-$pkgver-py3-none-any.whl"

      # hatchling doesn't support installation outside of the python root, so we need to install data files manually
      local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
      cp -a "$pkgdir$site_packages/$pkgname/data/"* "$pkgdir/usr/"
      rm -r "$pkgdir$site_packages/$pkgname/data"

      # keep usr/share configs as reference and copy them to /etc
      install -Dm644 "$pkgdir/usr/share/$pkgname/settings/ahriman.ini" "$pkgdir/etc/ahriman.ini"
      install -Dm644 "$pkgdir/usr/share/$pkgname/settings/ahriman.ini.d/logging.ini" "$pkgdir/etc/ahriman.ini.d/logging.ini"

      install -Dm644 "$srcdir/$pkgname.sysusers" "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
      install -Dm644 "$srcdir/$pkgname.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
